<!DOCTYPE html>
<!-- saved from url=(0022)https://jason.today/rc -->
<html lang="en">

<head>
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">

  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  <title>Building Real-Time Global Illumination: Radiance Cascades</title>

  <script src="./three.min.js"></script>
  <style>
    body {
      margin: 0;
    }
  </style>
</head>

<body>
  <div id="radiance-cascades-canvas">
    <div style="display: flex; gap: 20px;">
      <div id="radiance-cascades-canvas-canvas-container"><canvas __spector_context_type="webgl2" width="1024"
          height="1024" style="display: block; width: 512px; height: 512px;"></canvas></div>
    </div>
  </div>

  <script id="rc-fragment2" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
uniform vec2 resolution;
uniform sampler2D sceneTexture;
uniform sampler2D distanceTexture;
uniform sampler2D lastTexture;
uniform vec2 cascadeExtent;
uniform float cascadeCount;
uniform float cascadeIndex;
uniform float basePixelsBetweenProbes;
uniform float cascadeInterval;
uniform float rayInterval;
uniform bool addNoise;
uniform bool enableSun;
uniform float sunAngle;
uniform float srgb;
uniform float firstCascadeIndex;
uniform float lastCascadeIndex;
uniform float baseRayCount;

in vec2 vUv;
out vec4 FragColor;

const float PI = 3.14159265;
const float TAU = 2.0 * PI;
const float goldenAngle = PI * 0.7639320225;
const float sunDistance = 1.0;

const vec3 skyColor = vec3(0.2, 0.24, 0.35) * 6.0;
const vec3 sunColor = vec3(0.95, 0.9, 0.8) * 4.0;

const vec3 oldSkyColor = vec3(0.02, 0.08, 0.2);
const vec3 oldSunColor = vec3(0.95, 0.95, 0.9);

vec3 oldSunAndSky(float rayAngle) {
  // Get the sun / ray relative angle
  float angleToSun = mod(rayAngle - sunAngle, TAU);

  // Sun falloff based on the angle
  float sunIntensity = smoothstep(1.0, 0.0, angleToSun);

  // And that's our sky radiance
  return oldSunColor * sunIntensity + oldSkyColor;
}

vec3 sunAndSky(float rayAngle) {
    // Get the sun / ray relative angle
    float angleToSun = mod(rayAngle - sunAngle, TAU);

    // Sun falloff
    float sunIntensity = pow(max(0.0, cos(angleToSun)), 4.0 / sunDistance);

    return mix(sunColor * sunIntensity, skyColor, 0.3);
}

float rand(vec2 co) {
    return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

vec4 safeTextureSample(sampler2D tex, vec2 uv, float lod) {
    vec4 color = texture(tex, uv);
    return vec4(color.rgb, color.a);
}

vec4 colorSample(sampler2D tex, vec2 uv, bool srgbSample) {
    vec4 color = texture(tex, uv);
    if (!srgbSample) {
      return color;
    }
    return vec4(pow(color.rgb, vec3(srgb)), color.a);
}

vec4 colorWSample(sampler2D tex, vec2 uv, bool srgbSample) {
    vec3 color = texture(tex, uv).gba;
    return vec4(0.0, color);
}

vec4 raymarch(
  vec2 normalizedPoint, vec2 delta, float scale, vec2 oneOverSize, vec2 interval, float intervalLength, float minStepSize
) {
    vec2 rayUv = normalizedPoint + delta * interval;
    if (floor(rayUv) != vec2(0.0)) return vec4(0);

    vec2 pre = delta * scale * oneOverSize;

    int safety = 0;

    for (float dist = 0.0; dist < intervalLength && safety < 100; safety++) {
        ivec2 texel = ivec2(int(rayUv.x * resolution.x), int(rayUv.y * resolution.y));
        float df = texelFetch(distanceTexture, texel, 0).r;

        if (df <= minStepSize) {
          ivec2 texel = ivec2(int(rayUv.x * resolution.x), int(rayUv.y * resolution.y));
          vec4 color = vec4(texelFetch(distanceTexture, texel, 0).gba, 1.0);
          color.rgb = pow(color.rgb, vec3(srgb));
          return color;
        }

        dist += df * scale;
        if (dist >= intervalLength) break;

        rayUv += pre * df;
        if (floor(rayUv) != vec2(0.0)) break;
    }

    return vec4(0);
}

vec4 merge(vec4 currentRadiance, float index, vec2 position, float spacingBase) {
    // Early return conditions
    if (currentRadiance.a > 0.0 || cascadeIndex >= cascadeCount - 1.0) {
      return currentRadiance;
    }
  
    float upperSpacing = pow(spacingBase, cascadeIndex + 1.0);
    vec2 upperSize = floor(cascadeExtent / upperSpacing);
    vec2 upperPosition = vec2(
      mod(index, upperSpacing),
      floor(index / upperSpacing)
    ) * upperSize;
  
    // Calculate the position within the upper cascade cell
    vec2 offset = (position + 0.5) / spacingBase;
  
    vec2 clamped = clamp(offset, vec2(1.0), upperSize - 1.0);
    vec2 upperProbePosition = (upperPosition + clamped) / cascadeExtent;
  
    // Sample from the next cascade
    vec4 upperSample = textureLod(
      lastTexture, upperProbePosition,
      basePixelsBetweenProbes == 1.0 ? 0.0 : log(basePixelsBetweenProbes) / log(2.0)
    );
  
    return currentRadiance + upperSample;
  }
  
  void main() {
      vec2 coord = floor(vUv * cascadeExtent);
  
      if (cascadeIndex == 0.0) {
        ivec2 texel = ivec2(int(vUv.x * resolution.x), int(vUv.y * resolution.y));
        vec4 color = texelFetch(distanceTexture, texel, 0);
        if (color.r < 0.0) {
            FragColor = vec4(color.gba, 1.0);
            return;
        }
      }
  
      float base = baseRayCount;
      float rayCount = pow(base, cascadeIndex + 1.0);
      float sqrtBase = sqrt(baseRayCount);
      float spacing = pow(sqrtBase, cascadeIndex);
  
      // Hand-wavy rule that improved smoothing of other base ray counts
      float modifierHack = base < 16.0 ? pow(basePixelsBetweenProbes, 1.0) : sqrtBase;
  
      vec2 size = floor(cascadeExtent / spacing);
      vec2 probeRelativePosition = mod(coord, size);
      vec2 rayPos = floor(coord / size);
  
      float modifiedInterval = modifierHack * rayInterval * cascadeInterval;
  
      float start = cascadeIndex == 0.0 ? cascadeInterval : modifiedInterval;
      vec2 interval = (start * pow(base, (cascadeIndex - 1.0))) / resolution;
      float intervalLength = ((modifiedInterval) * pow(base, cascadeIndex)) * 30.0;
  
      vec2 probeCenter = (probeRelativePosition + 0.5) * basePixelsBetweenProbes * spacing;
  
      float preAvgAmt = baseRayCount;
  
      // Calculate which set of rays we care about
      float baseIndex = (rayPos.x + (spacing * rayPos.y)) * preAvgAmt;
      // The angle delta (how much it changes per index / ray)
      float angleStep = TAU / rayCount;
  
      // Can we do this instead of length?
      float scale = min(resolution.x, resolution.y);
      vec2 oneOverSize = 1.0 / resolution;
      float minStepSize = min(oneOverSize.x, oneOverSize.y) * 0.5;
      float avgRecip = 1.0 / (preAvgAmt);
  
      vec2 normalizedProbeCenter = probeCenter * oneOverSize;
  
      vec4 totalRadiance = vec4(0.0);
      float noise = addNoise
          ? rand(vUv * (cascadeIndex + 1.0))
          : 0.0;
  
      for (int i = 0; i < int(preAvgAmt); i++) {
        float index = baseIndex + float(i);
        float angle = (index + 0.5 + noise) * angleStep;
        vec2 rayDir = vec2(cos(angle), -sin(angle));
  
        // Core raymarching!
        vec4 raymarched = raymarch(
          normalizedProbeCenter, rayDir, scale, oneOverSize, interval, intervalLength, minStepSize
        );
  
        // Merge with the previous layer
        vec4 merged = merge(raymarched, index, probeRelativePosition, sqrtBase);
  
        // If enabled, apply the sky radiance
        if (enableSun && cascadeIndex == cascadeCount - 1.0) {
          merged.rgb = max(addNoise ? oldSunAndSky(angle) : sunAndSky(angle), merged.rgb);
        }
  
        totalRadiance += merged * avgRecip;
      }
  
      FragColor = vec4(
        (cascadeIndex > firstCascadeIndex)
          ? totalRadiance.rgb
          : pow(totalRadiance.rgb, vec3(1.0 / srgb)),
        totalRadiance.a
      );
    }

  </script>
  <script id="rc-fragment" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
uniform vec2 resolution;
uniform sampler2D sceneTexture;
uniform sampler2D distanceTexture;
uniform sampler2D lastTexture;
uniform vec2 cascadeExtent;
uniform float cascadeCount;
uniform float cascadeIndex;
uniform float basePixelsBetweenProbes;
uniform float cascadeInterval;
uniform float rayInterval;
uniform bool addNoise;
uniform bool enableSun;
uniform float sunAngle;
uniform float srgb;
uniform float firstCascadeIndex;
uniform float lastCascadeIndex;
uniform float baseRayCount;

in vec2 vUv;
out vec4 FragColor;

const float PI = 3.14159265;
const float TAU = 2.0 * PI;
const float goldenAngle = PI * 0.7639320225;
const float sunDistance = 1.0;

const vec3 skyColor = vec3(0.2, 0.24, 0.35) * 6.0;
const vec3 sunColor = vec3(0.95, 0.9, 0.8) * 4.0;

const vec3 oldSkyColor = vec3(0.02, 0.08, 0.2);
const vec3 oldSunColor = vec3(0.95, 0.95, 0.9);

vec3 oldSunAndSky(float rayAngle) {
  // Get the sun / ray relative angle
  float angleToSun = mod(rayAngle - sunAngle, TAU);

  // Sun falloff based on the angle
  float sunIntensity = smoothstep(1.0, 0.0, angleToSun);

  // And that's our sky radiance
  return oldSunColor * sunIntensity + oldSkyColor;
}

vec3 sunAndSky(float rayAngle) {
    // Get the sun / ray relative angle
    float angleToSun = mod(rayAngle - sunAngle, TAU);

    // Sun falloff
    float sunIntensity = pow(max(0.0, cos(angleToSun)), 4.0 / sunDistance);

    return mix(sunColor * sunIntensity, skyColor, 0.3);
}

float rand(vec2 co) {
    return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

vec4 safeTextureSample(sampler2D tex, vec2 uv, float lod) {
    vec4 color = textureLod(tex, uv, lod);
    return vec4(color.rrr, 1.0);
}

vec4 colorSample(sampler2D tex, vec2 uv, float lod, bool srgbSample) {
    vec4 color = textureLod(tex, uv, lod);
    if (!srgbSample) {
      return color;
    }
    return vec4(pow(color.rgb, vec3(srgb)), color.a);
}

vec4 colorWSample(sampler2D tex, vec2 uv, float lod, bool srgbSample) {
    vec3 color = textureLod(tex, uv, lod).gba;
    return vec4(0.0, color);
}

vec4 raymarch(
  vec2 normalizedPoint, vec2 delta, float scale, vec2 oneOverSize, vec2 interval, float intervalLength, float minStepSize
) {
    vec2 rayUv = normalizedPoint + delta * interval;
    if (floor(rayUv) != vec2(0.0)) return vec4(0);

    vec2 pre = delta * scale * oneOverSize;

    int safety = 0;

    for (float dist = 0.0; dist < intervalLength && safety < 100; safety++) {
        float df = safeTextureSample(distanceTexture, rayUv, 0.0).r;

        if (df <= minStepSize) {
          vec4 sampleColor = colorWSample(distanceTexture, rayUv, 0.0, true);
          return sampleColor;
        }

        dist += df * scale;
        if (dist >= intervalLength) break;

        rayUv += pre * df;
        if (floor(rayUv) != vec2(0.0)) break;
    }

    // divide by hits?
    return vec4(0);
}

vec4 merge(vec4 currentRadiance, float index, vec2 position, float spacingBase) {
    // Occluders / Largest cascade
    if (currentRadiance.a > 0.0 || cascadeIndex >= cascadeCount - 1.0) {
      return currentRadiance;
    }

    float upperSpacing = pow(spacingBase, cascadeIndex + 1.0);
    vec2 upperSize = floor(cascadeExtent / upperSpacing);
    vec2 upperPosition = vec2(
      mod(index, upperSpacing), floor(index / upperSpacing)
    ) * upperSize;

    vec2 offset = (position + 0.5) / spacingBase;

    // Sample in-between the 4 probes for pre-averaging
    vec2 clamped = clamp(offset, vec2(1.0), upperSize - 1.0);
    vec2 samplePosition = (upperPosition + clamped);
    vec4 upperSample = colorSample(
      lastTexture,
      samplePosition / cascadeExtent,
      0.0,
      false
    );

    vec3 c = currentRadiance.rgb + upperSample.rgb;


    return vec4(
      c,
      currentRadiance.a + upperSample.a
    );
}

void main() {
    vec2 coord = floor(vUv * cascadeExtent);

    if (cascadeIndex == 0.0) {
      vec4 color = colorWSample(sceneTexture, vUv, cascadeIndex, true);
      if (color.a > 0.0) {
          FragColor = vec4(
              pow(color.rgb, vec3(1.0 / srgb)),
              color.a
          );
          return;
      }
    }

    float base = baseRayCount;
    float rayCount = pow(base, cascadeIndex + 1.0);
    float sqrtBase = sqrt(baseRayCount);
    float spacing = pow(sqrtBase, cascadeIndex);

    // Hand-wavy rule that improved smoothing of other base ray counts
    float modifierHack = base < 16.0 ? 1.0 : sqrtBase;

    vec2 size = floor(cascadeExtent / spacing);
    vec2 probeRelativePosition = mod(coord, size);
    vec2 rayPos = floor(coord / size);

    float modifiedInterval = modifierHack * rayInterval * cascadeInterval;

    float start = cascadeIndex == 0.0 ? cascadeInterval : modifiedInterval;
    vec2 interval = (start * pow(base, (cascadeIndex - 1.0))) / resolution;
    float intervalLength = (modifiedInterval) * pow(base, cascadeIndex) * 30.0;

    vec2 probeCenter = (probeRelativePosition + 0.5) * basePixelsBetweenProbes * spacing;

    float preAvgAmt = baseRayCount;

    // Calculate which set of rays we care about
    float baseIndex = (rayPos.x + (spacing * rayPos.y)) * preAvgAmt;
    // The angle delta (how much it changes per index / ray)
    float angleStep = TAU / rayCount;

    // Can we do this instead of length?
    float scale = min(resolution.x, resolution.y);
    vec2 oneOverSize = 1.0 / resolution;
    float minStepSize = min(oneOverSize.x, oneOverSize.y) * 0.5;
    float avgRecip = 1.0 / (preAvgAmt);

    vec2 normalizedProbeCenter = probeCenter * oneOverSize;

    vec4 totalRadiance = vec4(0.0);
    float noise = addNoise
        ? rand(vUv * (cascadeIndex + 1.0)) / (rayCount * 0.5)
        : 0.0;

    for (int i = 0; i < int(preAvgAmt); i++) {
      float index = baseIndex + float(i);
      float angle = (index + 0.5) * angleStep + noise;
      vec2 rayDir = vec2(cos(angle), -sin(angle));

      // Core raymarching!
      vec4 raymarched = raymarch(
        normalizedProbeCenter, rayDir, scale, oneOverSize, interval, intervalLength, minStepSize
      );

      // Merge with the previous layer
      vec4 merged = merge(raymarched, index, probeRelativePosition, sqrtBase);

      // If enabled, apply the sky radiance
      if (enableSun && cascadeIndex == cascadeCount - 1.0) {
        merged.rgb = max(addNoise ? oldSunAndSky(angle) : sunAndSky(angle), merged.rgb);
      }

      totalRadiance += merged * avgRecip;
    }

    FragColor = vec4(
      (cascadeIndex > firstCascadeIndex)
        ? totalRadiance.rgb
        : pow(totalRadiance.rgb, vec3(1.0 / srgb)),
      1.0
    );
}
</script>
<script src="./rcmini.js"></script>
</div>
</body>
</html>